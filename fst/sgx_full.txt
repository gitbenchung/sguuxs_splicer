Multichar_Symbols
+DEM
+IMP
+N
+NUM
+P
+PL
+PNN
+PRO
+PT
+PVB
+QUA
+SUB
-1PL
-1PL.II
-1SG.II
-2PL
-2PL.II
-2SG.II
-3.II
-3PL
-3PL.II
-3PL.INDP
-ATTR
-SX
-T
-TR
-VAL
.I
1=
1PL
1PL.III
1SG.III
2=
2PL
2PL.III
2SG
2SG.III
3.III
3=
3PL.III
=1.I
=2.I
=CN
=EPIS
=PN
=PR.EV
=PROX
=QUDD
=REPORT
=RESEM
=YNQ
@C.NUM@
@D.CAT.AUX@
@D.NUM.PL@
@D.NUM.SG@
@D.SI@
@D.VAL.BIGT@
@P.CAT.AUX@
@P.NUM.PL@
@P.NUM.SG@
@P.SI.1@
@P.SI.2@
@P.SI.3@
@P.VAL.BIGT@
@P.VAR.AE@
@P.VAR.GWIGU@
@P.VAR.HEHI@
@P.VAR.IMIX@
@P.VAR.XSKS@
@R.CAT.AUX@
ASSOC
AX
DIST
PL_
PN
PROX

LEXICON Root
RootNoun ;
RootModifier ;
RootAdverb ;
RootPrenoun ;
RootQuantifier ;
RootParticle ;
RootClauseModifier ;
RootNumber ;

LEXICON RootNoun
nts'i'its	Noun ;
nts'iidz	Noun ;
niya'ay		Noun ;
nigwaat		Noun ;
łgutx̱a'oo	Noun ;
waky		Noun ;
insiipnsk	Noun ;		
hana'ax̱	Noun ;
łguułgu	Noun ;
kw'ida'ts	Noun ;
łimkt'ii	Noun ;
daala	Noun ;
waa	Noun ;
waalp	Noun ;
waap	Noun ;
ts'ikts'ik	Noun ;
x̱aldaawx̱k	Noun ;
an'on	Noun ;
naks	Noun ;
kap	Noun ;
g̱oot	Noun ;
wüliilm tgwah	Noun ;
g̱aayt	Noun ;
hooya	Noun ;
g̱aws	Noun ;
ha'ligyet	Noun ;
ał`	Preposition ;
łaxi	Preposition ;
lax̱'oi	Preposition ;
ts'm	Preposition ;


LEXICON Noun
+N: 	NDeriv ;

LEXICON NDeriv
-T:^T		SeriesII ;
-VAL:^xw	SeriesII ;
-VAL:^xw	SX ;
			Resemb ;
			NInfl ;

LEXICON NInfl
-ATTR:^m	# ;
-ATTR:^a	# ;
		SeriesII ;
		SX ;
		Word ;

LEXICON Prenoun
+PNN:	#;
+PNN-:^	RootNoun;


LEXICON Quantifier
+QUA:	QuantificationalInfl;

LEXICON Number
+NUM:	QuantificationalInfl;

LEXICON QuantificationalInfl
	Word ;
	SX ;
    AgreeingNClitic ;
-1PL.II:^'m	Word ;
-2PL.II:^si'm	Word ;
-3PL.II:^diit	Word ;

LEXICON Particle
+PT:	# ;


LEXICON SeriesII
-1SG.II:^'y	Word ;
-1PL.II:^'m	Word ;
-2SG.II:^n		Word ;
-2PL.II:^si'm	Word ;
-3.II:^t		Word ;
-3PL.II:^diit	Word ;
	AgreeingNClitic ;

LEXICON AgreeingNClitic
[-3.II]=CN:^ł	# ;
[-3.II]=PN:^s	# ;
[-3.II]=PN:^s	DemonstrativeClitic ;

LEXICON SX
-SX:^it		Word ;
-SX:^it		Resemb ;

LEXICON daPlClitic
-3PL.INDP:^da	# ;

LEXICON SeriesIprocl
@P.SI.1@1.I=:@P.SI.1@n^	SeriesIable ;
@P.SI.2@2.I=:@P.SI.2@m^	SeriesIable ;

LEXICON SeriesIable
	RootModal ;
	RootSubordinator ;
	RootAuxiliary ;

LEXICON SeriesI
1.I:[na|n]	# ;
1PL.I:dip 	# ;
2.I:mi 		# ;
2.I:ma 		# ;
2PL.I:sim 	# ;

LEXICON Auxiliary
@P.CAT.AUX@+AUX:@P.CAT.AUX@	SeriesIencl ;
@P.CAT.AUX@+AUX:@P.CAT.AUX@	SecPosClitic ;
@P.CAT.AUX@+AUX:@P.CAT.AUX@	CNClitic ;
@P.CAT.AUX@+AUX:@P.CAT.AUX@	SX ;

LEXICON SeriesIencl
@D.SI@=1.I:@D.SI@^n			# ;	! used = instead of ^ to condition special epenthesis
@D.SI@=2.I:@D.SI@^m			# ;
!=2.I=2PL.I:^m^sim	# ;
	SeriesIencl3 ;

LEXICON SeriesIencl3
@D.SI@=3.I:@D.SI@^t			# ;
				# ;
LEXICON Root
	Exceptions ;

LEXICON Exceptions
w$il+VI-2SG.II:win		Word ;
w$il+VI-2PL.II:wisi'm	Word ;
w$il+VI=CN:wihl		# ;
w$il+VI[-3.II]=CN:wihl	# ;
w$il+VI[-3.II]=PN:wis	# ;

y$ee+VI-2SG.II:yin 	Word ;

w$in+SUB=1.I:wina		# ;
n$ee+AUX=d$im+MOD:nem	# ;
d$im+MOD=2.I:dima	# ;

2SG+OBL:lun 		Word ;
3.III+PRO=CN:'nihl		# ;


LEXICON Root
	Preposition ;
	Conjunction ;
	Pronoun ;
	NProclitic ;
	SeriesI ;
	SeriesIprocl ;

LEXICON Preposition
g_$o'o+P:g_$o'o	# ;
g_$o'o+P:g_$o'o	SeriesII ;
g_$oo+P:g_$oo	# ;
g_$oo+P:g_$oo	SeriesII ;
g_$a'a+P:g_$a'a	# ;
g_$a'a+P:g_$a'a	SeriesII ;
'$a+P:'$a 			# ;
'$a+P:'$a 			AgreeingNClitic ;
'$a+P-OBL:'$a^l$oo		AgreeingNClitic ;

LEXICON Conjunction
g_an+CNJ:g_an		NextNClitic ;
'$oo+CNJ:'$oo			# ;

LEXICON Pronoun
1SG.III+PRO:'nii'y 	Word ;
1PL.III+PRO:'nuu'm	Word ;
2SG.III+PRO:'niin 	Word ;
2PL.III+PRO:'nisi'm 	Word ;
3.III+PRO:'nit 		Word ;
3PL.III+PRO:'nidiit 	Word ;
1SG+OBL:loo'y 	Word ;
1PL+OBL:loo'm	Word ;
2SG+OBL:loon 	Word ;
2PL+OBL:loosi'm 	Word ;
3+OBL:loot 		Word ;
3PL+OBL:loodiit 	Word ;

LEXICON NProclitic
PN:t		# ;
PN:t		DemonstrativeClitic ;
ASSOC:dip	# ;
ASSOC:dip	DemonstrativeClitic ;

LEXICON DemonstrativeClitic
=PROX+DEM:=un	Word ;
=DIST+DEM:=ust	Word ;

LEXICON Operator
FOC+OP:dii		SeriesIencl ;
FOC+OP:dii		PNClitic;
PART+OP:hli		SeriesIencl ;
AX+OP:an 		SeriesIencl3 ;
DWID+OP:l$igi	PNClitic ;

LEXICON Word
	SecPosClitic ;
@D.CAT.AUX@	PhraseClitic ;
@D.CAT.AUX@	SentFinalClitic ;

LEXICON PhraseClitic
=PROX:^si 	NextNClitic ;
=PROX:^sa 	NextNClitic ;
=PR.EV:^gi 	NextNClitic ;
		 	NextNClitic ;

LEXICON Resemb
=RESEM:^ii 	CNClitic ;

LEXICON NextNClitic
	CNClitic ;
	PNClitic ;

LEXICON CNClitic
=CN:^hl	# ;
		# ;

LEXICON PNClitic
=PN:^t	# ;
		# ;

LEXICON SecPosClitic
@R.CAT.AUX@=EPIS:@R.CAT.AUX@^ima'a	SeriesIencl ;
@R.CAT.AUX@=EPIS:@R.CAT.AUX@^imaa	SeriesIencl ;
=EPIS:^ima'a	CNClitic ;
=EPIS:^imaa	CNClitic ;
=REPORT:^g_at	CNClitic ;

! still need to test these e.g. with vowel hiatus interaction
