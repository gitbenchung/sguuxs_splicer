define Vowel a | e | i | o | u | ü | "_" ;
define Consonant p | {p'} | m | s | c h | h | t | {t'} | k | {k'} | n | ł | l | k w | k y | x | x w | x y | y | ẅ | ' | "_" ;
define CLetter b | d | dz | g | g̱ | {g'} | g w | g y | "_" ;
define Sonorant m | n | l | w | y | {'m} | {'n} | {'l} | {'w} | {'y} | {'ẅ} ;
define Sibilant s | c h | {ts} | {ts'} ;
define InitialCons b | c h | d | d z | g | g w | g y | h | k | {k'} | k w | k y | l | {'l} | ł | m | n | {'n} | p | {p'} | s | t | {t'} | {ts} | {ts'} | w | {'w} | ẅ | {'ẅ} | x | x w | x y | y | {'y} ;

!--epenthesis, stop cluster mkt > mkit
define StopClusterEp    [..] -> i || [ p | b ] "^" _ t ,,
                        [..] -> i || [ k | g ] "^" _ t ,,
                        [..] -> i || [ t | d ] "^" _ t ;

!-- voicing rule, vowel adjacency
define VowelVoicing k -> g || _ "^" Vowel ,,
                    p -> b || _ "^" Vowel ,,
                    t -> d || _ "^" Vowel ,,
                    {x_} -> {g_} || _ "^" Vowel ;

!-- voicing rule, nasal adjacency
define NasalVoicing k -> g || _ "^" [ n | m ] ,,
                    p -> b || _ "^" [ n | m ] ,,
                    t -> d || _ "^" [ n | m ] ,,
                    {x_} -> {g_} || _ "^" [ n | m ] ;

!-- glide insertion, rounded vowel concatenation
define GlideInsertion  [..] -> y || Vowel _ "^" u ;

!--when Series II -sm follows a sibilant affricate/fricative, insert a Vowel
define SimHiatus [..] -> i || Sibilant "^" _ {sm} ;

!--delete a connective clitic following an identical phoneme
define DoubleFricative  s -> 0 || Sibilant "^" _ ,,
                        ł -> 0 || ł "^" _ ;

regex Lexicon ;
define Stems ;

regex Stems
    .o. StopClusterEp
    .o. VowelVoicing
    .o. NasalVoicing
    .o. GlideInsertion
    .o. SimHiatus
    .o. DoubleFricative
    ;
define MessyWords ;

!--delete stress marking
define DeleteStress  "$" -> 0 ;

!--delete morpheme boundaries from surface form
define Cleanup  "^" | "=" | "~" -> 0 ;

regex MessyWords
    .o. DeleteStress
    .o. Cleanup
;

define CleanWords ;
read regex CleanWords ;
